AC_PREREQ(2.50)

AC_INIT(util/util.h)
AM_INIT_AUTOMAKE(jadc2s,0.9cvs)
AM_CONFIG_HEADER(config.h)

dnl helper macros
sinclude(ac-helpers/ac_define_dir.m4)
sinclude(ac-helpers/openssl.m4)

dnl for developers
AC_MSG_CHECKING(if developer mode enabled)
AC_ARG_ENABLE(developer, AC_HELP_STRING([--enable-developer],
                    [Developer mode]),
              developer=yes)

if test x-$developer = "x-yes" ; then
    AC_MSG_RESULT(yes)
    CFLAGS="$CFLAGS -Wall -g"
else
    AC_MSG_RESULT(no)
fi


dnl Check for programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AM_ICONV

AC_DISABLE_STATIC
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

dnl use libtool to compile checks (particularly lib checks), mostly so we don't have to worry about how/if the os supports -R
ac_link="${SHELL} ${srcdir}/libtool --mode=link $ac_link"

dnl headers we need
AC_HEADER_STDC

dnl static builds
AC_MSG_CHECKING(if static builds enabled)
AC_ARG_ENABLE(all-static, AC_HELP_STRING([--enable-all-static], [Build static binaries]), all_static=yes, all_static=no)
if test "x-$all_static" = "x-yes" ; then
    LDFLAGS="$LDFLAGS -Wl,-static -static"
fi
AC_MSG_RESULT($all_static)

AC_MSG_CHECKING(if partial static builds enabled)
AC_ARG_ENABLE(partial-static, AC_HELP_STRING([--enable-partial-static], [Build partially static binaries]), partial_static=yes, partial_static=no)
if test "x-$partial_static" = "x-yes" ; then
    LDFLAGS="$LDFLAGS -Wl,-lc,-static -static"
fi
AC_MSG_RESULT($partial_static)

dnl solaris has socket functions in libsocket
AC_CHECK_FUNC(socket, have_libsocket=yes, have_libsocket=no)
if test "$have_libsocket" = "yes"; then
    AC_DEFINE(HAVE_LIBSOCKET, 1, [Define if socket and associated functions are available.])
else
    AC_CHECK_LIB(socket, socket, have_libsocket=yes, have_libsocket=no)
    if test "$have_libsocket" = "yes"; then
        AC_DEFINE(HAVE_LIBSOCKET, 1, [Define if socket and associated functions are available.])
        LIBS="$LIBS -lsocket"
    fi
fi
if test "$have_libsocket" = "no" ; then
    AC_MSG_ERROR([Couldn't find required function socket]);
fi


dnl and other name things in libnsl
AC_CHECK_FUNC(inet_ntop, have_libnsl=yes, have_libnsl=no)
if test "$have_libnsl" = "yes"; then
    AC_DEFINE(HAVE_LIBNSL, 1, [Define if inet_ntop and associated functions are available.])
else
    AC_CHECK_LIB(nsl, inet_ntop, have_libnsl=yes, have_libnsl=no)
    if test "$have_libnsl" = "yes"; then
        AC_DEFINE(HAVE_LIBNSL, 1, [Define if inet_ntop and associated functions are available.])
        LIBS="$LIBS -lnsl"
    fi
fi
if test "$have_libnsl" = "no" ; then
    AC_MSG_ERROR([Couldn't find required function inet_ntop]);
fi

dnl MIO backend
mio_backend="poll"
AC_MSG_CHECKING(which mio backend to use)
AC_ARG_ENABLE(mio, AC_HELP_STRING([--enable-mio=BACKEND], [Select the mio backend (default: poll)]), 
            mio_backend=$enableval)

case x-$mio_backend in
    x-poll)
        AC_MSG_RESULT(poll)
        AC_DEFINE(MIO_POLL,,[MIO poll backend]);;
    x-select)
        AC_MSG_RESULT(select)
        AC_DEFINE(MIO_SELECT,,[MIO select backend]);;
    *)
        AC_MSG_ERROR([Unknown MIO backend: $mio_backend]);;
esac;


dnl debugging
AC_MSG_CHECKING(if debug messages wanted)
AC_ARG_ENABLE(debug, AC_HELP_STRING([--enable-debug],
                    [Enable debug messages]),
              debug=yes)

if test x-$debug = "x-yes" ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(DEBUG,,[app debug enabled])
else
    AC_MSG_RESULT(no)
fi

AC_SUBST(DEBUG)

AC_MSG_CHECKING(if pool debugging wanted)
AC_ARG_ENABLE(pool_debug, AC_HELP_STRING([--enable-pool-debug], [Enable pool debugging]), pool_debug=yes)
if test x-$pool_debug = "x-yes" ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(POOL_DEBUG,,[pool debug enabled])
else
    AC_MSG_RESULT(no)
fi

AC_SUBST(POOL_DEBUG)

dnl check where to log
AC_MSG_CHECKING(if logging to syslog wanted)
AC_ARG_ENABLE(syslog, AC_HELP_STRING([--enable-syslog], [Enable logging to syslog]), syslog=yes)
if test x-$syslog = "x-yes" ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(USE_SYSLOG,,[logging to syslog enabled])
else
    AC_MSG_RESULT(no)
fi

AC_SUBST(USE_SYSLOG)


dnl check if SSL/TLS is requested
AC_MSG_CHECKING(if OpenSSL is requsted)
AC_ARG_ENABLE(ssl, AC_HELP_STRING([--enable-ssl], [Enable protection with SSL/TLS]), ssl=yes)
if test x-$ssl = "x-yes" ; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(USE_SSL,,[SSL/TLS with openssl requested])
    JADC2S_LIB_OPENSSL(0,9,6,2)
    CFLAGS="$CFLAGS $JADC2S_SSL_INCLUDES"
    LDFLAGS="$LDFLAGS $JADC2S_SSL_LIBS"
else
    AC_MSG_RESULT(no)
fi

AC_SUBST(USE_SSL)

dnl define where the configuration file is located
AC_DEFINE_DIR(CONFIG_DIR,sysconfdir,[where the configuration file can be found])

dnl Create the makefiles
AC_OUTPUT(Makefile \
	  mio/Makefile \
	  util/Makefile \
	  xmlparse/Makefile \
	  ac-helpers/Makefile)
